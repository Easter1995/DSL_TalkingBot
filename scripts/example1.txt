main {
    output "您好，您可以对我描述您的问题"
    goto menu
}
menu {
    input
    for /你好|您好/ goto greet
    for /余额/ goto balance
    for /激活/ goto active
    for /充值|存款/ goto recharge
    for /退出|再见/ goto exit
    default goto default
}
greet {
    output "您好，很高兴见到你。"
    goto menu
}
balance {
    output "您的银行卡余额是 ${balance} 元"
    goto menu
}
active {
    output "您已成功激活银行卡，送您 20 元"
    save balance
    eval balance = 20
    goto menu
}
recharge {
    output "请输入您的存款金额"
    input
    save amount
    eval balance = Number(balance) + Number(amount)
    output "您已成功存款，现在余额为 ${balance} 元"
    goto menu
}
exit {
    output "再见，祝您今天愉快"
    exit
}
default {
    output "对不起，我听不懂您在说什么"
    goto menu
}